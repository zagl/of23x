#!/bin/bash

name=$1

awk '
BEGIN {
    print "# vtk DataFile Version 2.0"
    print "'$name'"
    print "ASCII"
    print "DATASET POLYDATA"
}
/^v/ {
    points[nPoints++] = $2 " " $3 " " $4
}
/^f/ {
    faces[nFaces++] = "3 " $2-1 " " $3-1 " " $4-1
}
/^[0-9-]/ {
    values[nValues++] = $4
}
END {
    print "POINTS", nPoints, "float"
    for ( i=0; i<nPoints; i++  )
        print points[i]
    print "POLYGONS", nFaces, nFaces*4
    for ( i=0; i<nFaces; i++  )
        print faces[i]
    print "POINT_DATA", nPoints
    print "FIELD attributes 1"
    print "T 1", nPoints, "float"
    for ( i=0; i<nValues; i++  )
    {
        printf "%s ", values[i]
        if ( (i+1)%10 == 0 )
            print ""
    }
}
' postProcessing/sets/${name}/10000/${name}_T.xy constant/triSurface/${name}F.obj
